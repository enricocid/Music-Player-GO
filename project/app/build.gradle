apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId "com.iven.musicplayergo"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 162
        versionName "4.4.0"

        vectorDrawables.useSupportLibrary true
    }

    //retrieve version number
    applicationVariants.all { variant ->
        resValue "string", "versionName", variant.versionName
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "@string/app_name_release"
        }
        debug {
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "@string/app_name_debug"
            debuggable true
        }
    }

    // Configure only for each module that uses Java 8
    // language features (either in its source code or
    // through dependencies).
    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // For Kotlin projects
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    lintOptions {
        disable 'MissingTranslation', 'AllowBackup',
                'GoogleAppIndexingWarning'
    }

    packagingOptions {
        exclude 'META-INF/*'
    }
}

dependencies {
// Kotlin
    //https://github.com/JetBrains/kotlin/blob/master/ChangeLog.md
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    //https://github.com/Kotlin/kotlinx.coroutines/blob/master/CHANGES.md
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'

// Support
    //https://developer.android.com/jetpack/androidx/versions
    implementation 'androidx.activity:activity-ktx:1.1.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.fragment:fragment-ktx:1.2.5'
    implementation 'androidx.media:media:1.2.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    //https://github.com/material-components/material-components-android/releases
    implementation 'com.google.android.material:material:1.3.0'

// Third-party
    //https://github.com/square/moshi/releases
    implementation 'com.squareup.moshi:moshi:1.11.0'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.11.0'

    //https://github.com/reddit/IndicatorFastScroll/releases
    implementation 'com.reddit:indicator-fast-scroll:1.3.0'

    //https://github.com/afollestad/material-dialogs/releases
    implementation 'com.afollestad.material-dialogs:core:3.3.0'
    implementation 'com.afollestad.material-dialogs:bottomsheets:3.3.0'

    //https://github.com/afollestad/recyclical/releases
    implementation 'com.afollestad:recyclical:1.1.1'

    //https://github.com/beworker/edge-to-edge/blob/master/CHANGELOG.md
    implementation 'de.halfbit:edge-to-edge:0.10'

    //https://github.com/coil-kt/coil/releases
    implementation 'io.coil-kt:coil:1.1.0'
}
